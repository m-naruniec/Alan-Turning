comment "#" ;
comment "//" ;
comment "/*" "*/" ;

Prog. Prog ::= Stmt ; --ok

-- Declarations

DDflt. Decl  ::= Type Ident ; --ok
--DVal.  Decl  ::= Type Ident "=" Exp ;
DProc. Decl  ::= "proc" Ident "(" [Param] ")" "{" Stmt "}" ;
Param. Param ::= Type Ident ;

separator  Param "," ;
terminator Decl ";" ;


-- Types

TInt.  Type ::= "int" ;
TBool. Type ::= "bool" ;

-- Statements

SSeq.   Stmt ::= Stmt ";" Stmt1 ; --ok

SLeft.  Stmt1 ::= "left" Stmt1 ; --ok
SRight. Stmt1 ::= "right" Stmt1 ; --ok

SSkip.  Stmt1 ::= "skip" ; --ok
SIf.    Stmt1 ::= "if" "(" Exp ")" "{" Stmt "}" ; --ok
SIfte.  Stmt1 ::= "if" "(" Exp ")" "{" Stmt "}" "else" "{" Stmt "}" ; --ok
STurn.  Stmt1 ::= "turn" ; --ok
SExp.   Stmt1 ::= Exp ; --ok
SPrint. Stmt1 ::= "print" Exp ; --ok
SBlock. Stmt1 ::= "{" Decl Stmt "}" ;

SProc. Stmt1 ::= Ident "(" [Arg] ")" ;
AVal.  Arg  ::= Exp ;
ARef.  Arg  ::= "ref" Ident ;

separator Arg "," ;

coercions Stmt 1 ;

-- Expressions

EAss.     Exp ::= Ident OpAss Exp ;
OpAss.    OpAss ::= "=" ;
OpAddAss. OpAss ::= "+=" ;
OpSubAss. OpAss ::= "-=" ;
OpMulAss. OpAss ::= "*=" ;
OpDivAss. OpAss ::= "/=" ;
OpModAss. OpAss ::= "%=" ;

EIfte. Exp ::= Exp1 "?" Exp ":" Exp1 ;

EOr.  Exp1 ::= Exp1 "||" Exp2 ;

EAnd. Exp2 ::= Exp2 "&&" Exp3 ;

EComp. Exp3 ::= Exp3 OpComp Exp4 ;
OpEq.  OpComp ::= "==" ;
OpNeq. OpComp ::= "!=" ;

EOrd.  Exp4 ::= Exp4 OpOrd Exp5 ;
OpLT.  OpOrd ::= "<" ;
OpLEq. OpOrd ::= "<=" ;
OpGT.  OpOrd ::= ">" ;
OpGEq. OpOrd ::= ">=" ;

EAdd.    Exp5 ::= Exp5 OpAdd Exp6 ;
OpPlus.  OpAdd ::= "+" ;
OpMinus. OpAdd ::= "-" ;

EMul.    Exp6 ::= Exp6 OpMul Exp7 ;
OpTimes. OpMul ::= "*" ;
OpDiv.   OpMul ::= "/" ;
OpMod.   OpMul ::= "%" ;

EInv.  Exp7 ::= OpInv Exp8 ; --ok
OpNot. OpInv ::= "!" ; --ok
OpNeg. OpInv ::= "-" ; --ok

EVar.   Exp8 ::= Ident ; --ok
EInt.   Exp8 ::= Integer ; --ok
ETrue.  Exp8 ::= "true" ; --ok
EFalse. Exp8 ::= "false" ; --ok

EPreOp.  Exp8 ::= OpPrePost Ident ; --ok
EPostOp. Exp8 ::= Ident OpPrePost ; --ok
OpInc.   OpPrePost ::= "++" ; --ok
OpDec.   OpPrePost ::= "--" ; --ok

coercions Exp 8 ;

